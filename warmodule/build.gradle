plugins {
    id "net.minecraftforge.gradle.forge" version "2.0.2"
}

ext.config = parseConfig(file('build.conf'))

version = config.version
group = config.groupId
archivesBaseName = config.moduleId

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "${config.mcVersion}-${config.forgeVersion}"
    runDir = "run"
    mappings = "snapshot_20141130"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'module.ini'
        expand 'version':project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'module.ini'
    }
}

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}
